import subprocess
import versionInfo

Import([
	'env',
	'outFilePrefix',
	'isStoreSubmition',
])

def getCertPublisher(env):
	certFile=env.get('certFile')
	if not certFile:
		return env['publisher']
	certPassword=env.get('certPassword','')
	cmd=['certutil','-dump','-p',certPassword,File('#'+certFile).abspath.replace('/','\\')]
	lines=subprocess.check_output(cmd).splitlines()
	linePrefix='Subject: '
	for line in lines:
		if line.startswith(linePrefix):
			subject=line[len(linePrefix):].rstrip()
			return subject

packageName="NVAccessLimited.NVDANonVisualDesktopAccess"
packageVersion="%s.%s.%s.%s"%(versionInfo.version_year,versionInfo.version_major,env['version_build'],0)
if isStoreSubmition:
	packageFileName=outFilePrefix+"_storeSubmition.appx"
	packagePublisher="CN=83B1DA31-9B66-442C-88AB-77B4B815E1DE"
	packagePublisherDisplayName="NV Access Limited"
	productName="NVDA Screen Reader (Windows Store Edition)" 
else:
	packageFileName=outFilePrefix+"_sideLoadable.appx"
	packagePublisher=getCertPublisher(env)
	packagePublisherDisplayName=env['publisher']
	productName="NVDA Screen Reader (Windows Desktop Bridge Edition)" 

signExec=env['signExec'] if env['certFile'] else None

# Files from NVDA's distribution that cannot be included in the appx due to policy or security restrictions
excludedDistFiles=[
	'nvda_eoaProxy.exe',
	'nvda_service.exe',
	'nvda_slave.exe',
	'nvda_noUiAccess.exe',
	'lib/IAccessible2Proxy.dll',
	'lib/minHook.dll',
	'lib/NVDAHelperRemote.dll',
	'lib/VBufBackend_adobeAcrobat.dll',
	'lib/VBufBackend_adobeFlash.dll',
	'lib/VBufBackend_gecko_ia2.dll',
	'lib/VBufBackend_lotusNotesRichText.dll',
	'lib/VBufBackend_mshtml.dll',
	'lib/VBufBackend_webKit.dll',
	'lib64',
	'uninstall.exe',
]

# Create an appx manifest with version and publisher etc all filled in 
manifest=env.Substfile(
	"AppxManifest.xml",
	'manifest.xml.subst',
	SUBST_DICT={
		'%packageName%':packageName,
		'%packageVersion%':packageVersion,
		'%packagePublisher%':packagePublisher,
		'%packagePublisherDisplayName%':packagePublisherDisplayName,
		'%productName%':productName,
		'%description%':versionInfo.description,
	},
)
# Make a copy of the dist dir produced by py2exe 
# And also place some extra appx specific images in there
appxContent=env.Command(
	target='content',
	source=[Dir("#dist"),Dir('#appx/appx_images'),manifest],
	action=[
		Delete("$TARGET"),
		Copy("$TARGET","${SOURCES[0]}"),
		Copy("${TARGET}\\appx_images","${SOURCES[1]}"),
		Copy("${TARGET}\\AppxManifest.xml","${SOURCES[2]}"),
	]+[Delete("${TARGET}/%s"%excludeFile) for excludeFile in excludedDistFiles],
)
# Ensure that it is always copied as we can't tell if dist changed 
env.AlwaysBuild(appxContent)
# Package the appx
appx=env.Command(packageFileName,appxContent,"makeappx pack /p $TARGET /d $SOURCE")
if signExec and not isStoreSubmition:
	env.AddPostAction(appx,signExec)

Return(['appx'])

